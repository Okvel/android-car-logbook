apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "by.liauko.siarhei.fcc"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 10
        versionName "3.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildToolsVersion = '28.0.3'
}

dependencies {
    def appcompat_version = "1.1.0"
    def material_version = "1.0.0"
    def recyclerview_version = "1.1.0"
    def constraintlayout_version = "1.1.3"
    def room_version = "2.2.3"
    def preference_version = "1.1.0"
    def play_service_auth_version = "17.0.0"
    def api_client_version = "1.26.0"
    def api_service_drive_version = "v3-rev136-1.25.0"
    def commons_io_version = "2.6"
    def gson_version = "2.8.6"
    def work_version = "2.3.0"

    // Test dependency versions
    def junit_version = "4.12"
    def test_runner_version = "1.2.0"
    def espresso_core_version = "3.2.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.preference:preference:$preference_version"

    // Google Play services Auth split
    implementation "com.google.android.gms:play-services-auth:$play_service_auth_version"
    // Google Api Client library and Android extension
    implementation "com.google.api-client:google-api-client:$api_client_version"
    implementation "com.google.http-client:google-http-client-gson:$api_client_version"
    implementation("com.google.api-client:google-api-client-android:$api_client_version") {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation("com.google.apis:google-api-services-drive:$api_service_drive_version") {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation "commons-io:commons-io:$commons_io_version"
    implementation "com.google.code.gson:gson:$gson_version"
    implementation("androidx.work:work-runtime-ktx:$work_version") {
        exclude group: 'com.google.guava'
    }

    api "com.google.android.material:material:$material_version"

    kapt "androidx.room:room-compiler:$room_version"

    testImplementation "junit:junit:$junit_version"
    testImplementation "androidx.room:room-testing:$room_version"

    androidTestImplementation "androidx.test:runner:$test_runner_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_core_version"
}
